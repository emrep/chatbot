! version = 2.0

// Udemy Scripts

// Substitutions
! sub the highest rated = popular
! sub the most expensive = expensive
! sub the cheapest = cheapest
! sub the newest = newest

! array coursenoun = course lesson lecture tutorial class
! array teachnoun = teach train tutor educate
! array modalverb = can could may will would
! array learnverb = learn know
! array listverb = list show display send
! array desireverb = desire want need
! array levelnoun = beginner intermediate expert
! array pricenoun = free paid
! array sortnoun = newest cheapest expensive popular

+ *
- I'm not sure I understand you fully. For instance you can try following messages:
^ * List web development courses.
^ * Show me free lectures.

+ [*] do you have * (@coursenoun)*
- <set topic=<star1>>Yes We have great <star1> <star2>s in our catalog. You may want to take a look at the following courses:

+ [*] (@coursenoun)[*]
- Hey! We have great <star>s in our catalog. Let me know what kind of courses you are looking for? For example;
^ * I am looking for PHP courses.
^ * Would you list Finance classes?

+ i (@desireverb) [to] [@learnverb] *
- <set topic=<star2>>Yes We have great tutorials related to <star2> in our catalog. You may want to take a look at the following courses:

+ (hello|hi|hey|howdy|hola|hai|yo)
- How do you do. Please let me know what kind of courses you want to take.
- Hi. How can I help you?

+ (@modalverb) you (@teachnoun) [me] *
- <set topic=<star3>>Yes of course we <star1>. The following courses can be helpful!!

+ (@modalverb) i (@learnverb) *
- <set topic=<star3>>Yes of course you <star1>. The following courses can be helpful!!

+ [@modalverb] [you] (@listverb) [me] * [@coursenoun]*
- <set topic=<star2>>Yes of course. Please take a look at the following courses:

+ [@modalverb] [you] (@listverb) [me] (@pricenoun) [*] [@coursenoun]*
- Definitely. Please take a look at the following the <star2> courses:

+ [@modalverb] [you] (@listverb) [me] (@levelnoun) [*] [@coursenoun]*
- Yes sure. Please take a look at the following the <star2> courses:

+ [@modalverb] [you] (@listverb) [me] (@sortnoun) [*] [@coursenoun]*
- Of course. Please take a look at the following the <star2> courses:

+ [@modalverb] [you] (@listverb) [me] (all|whole) [@coursenoun][*]
- Sorry!! I can not do that because this is so big data :(

+ [*] (looking for|search|seek) * [@coursenoun]*
- <set topic=<star2>>We have very nice suggestions. We hope the following courses are what you are exactly looking for: